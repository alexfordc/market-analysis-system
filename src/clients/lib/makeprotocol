#!/bin/bash
# Make a NetworkProtocol class for the TA server protocol.

eiffel_protocol_files ()
{
	echo ../../server1/network/gui_network_protocol.e \
		../../server1/network/network_protocol.e
}

files=$(eiffel_protocol_files)
awk 'BEGIN {
	print "/** Constants specifying the components of the TA network \n\
communication protocol */"
	print "public interface NetworkProtocol\n{"
}
/^feature/ {
	sub (/.*-- */, "")
	print "\t// " $0
}
/: *INTEGER/ {
	sub (/:/, "")
	print "\tint " $1 " = " $4 ";"
}
/: *STRING/ {
	sub (/:/, "")
	# Change Eiffel tab char to java tab char.
	if ($4 == "\"%T\"") {
		value = "\"\\t\""
	}
	# Change Eiffel newline char to java newline char.
	else if ($4 == "\"%N\"") {
		value = "\"\\n\""
	}
	else {
		value = $4
	}
	print "\tString " $1 " = " value ";"
}
END {
	print "}"
}' $files >../common/NetworkProtocol.java
